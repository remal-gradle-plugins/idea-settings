import groovy.json.JsonBuilder

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

buildscript {
    String rootGroupId = project.ext.rootGroupId = "name.remal.gradle-plugins.${rootProject.name}"
    String rootArtifactId = project.ext.rootArtifactId = rootProject.name
    String rootSnapshotVersion = project.ext.rootSnapshotVersion = '4-SNAPSHOT'
    dependencies {
        //classpath("$rootGroupId:$rootArtifactId:$rootSnapshotVersion") { version { strictly(rootSnapshotVersion) } }
        classpath 'name.remal.gradle-plugins.toolkit:build-logic:0.76.2'
    }
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

allprojects {
    group = project.rootGroupId
    version = project.rootSnapshotVersion
}

apply plugin: 'name.remal.toolkit.build-logic'

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

if (project.isNotRunningOnCi && gradle.startParameter.taskNames.contains('processIdeaSettingsDebug')) {
    tasks.named('processIdeaSettings') {
        File layoutJsonFile = rootProject.file('layout.json')
        doFirst {
            String jsonString = new JsonBuilder([ideaDirPath: rootProject.file('.idea').absolutePath]).toPrettyString()
            layoutJsonFile.setText(jsonString, 'UTF-8')
        }
        doLast {
            layoutJsonFile.delete()
        }
    }
}

tasks.register('processIdeaSettingsDebug') {
    group = 'debug'
    dependsOn('processIdeaSettings')
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

apply plugin: 'java-gradle-plugin'

dependencies {
    classesRelocation('net.sf.saxon:Saxon-HE:12.9') {
        exclude group: 'xml-apis'
        exclude group: 'org.apache.httpcomponents'
    }
    classesRelocation 'org.jdom:jdom2:2.0.6.1'
    classesRelocation 'org.xmlunit:xmlunit-core:2.11.0'

    api('gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:1.3') {
        exclude group: 'com.google.code.findbugs'
        exclude group: 'org.checkerframework'
        exclude group: 'com.google.errorprone'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
    }


    testImplementation 'org.json:json:20250517'
}

gradlePlugin {
    plugins {
        'name.remal.idea-settings' {
            id = 'name.remal.idea-settings'
            implementationClass = 'name.remal.gradle_plugins.idea_settings.IdeaSettingsPlugin'
            displayName = 'Configure IntelliJ IDEA project'
        }
    }
}

configurations.gradlePluginApiDependencies {
    exclude(group: 'com.google.code.findbugs')
    exclude(group: 'com.google.errorprone')
    exclude(group: 'com.google.guava')
    exclude(group: 'com.google.code.gson')
}
