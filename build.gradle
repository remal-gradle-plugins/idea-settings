import groovy.json.JsonBuilder

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

buildscript {
    String pluginGroupId = "name.remal.gradle-plugins.${rootProject.name}"
    String pluginArtifactId = rootProject.name
    String pluginSnapshotVersion = '2-SNAPSHOT'
    allprojects {
        group = pluginGroupId
        version = pluginSnapshotVersion
    }
    dependencies {
        //classpath("$pluginGroupId:$pluginArtifactId") { version { strictly(pluginSnapshotVersion) } }
        classpath 'name.remal.gradle-plugins.toolkit:build-logic:0.36.0'
    }
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

apply plugin: 'name.remal.toolkit.build-logic'

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

tasks.named('processIdeaSettings') {
    if (project.isNotRunningOnCi && gradle.startParameter.taskNames.contains('processIdeaSettingsDebug')) {
        doFirst {
            String jsonString = new JsonBuilder([ideaDirPath: rootProject.file('.idea').absolutePath]).toPrettyString()
            rootProject.file('layout.json').setText(jsonString, 'UTF-8')
        }
    }
}

tasks.create('processIdeaSettingsDebug') {
    group = 'debug'
    dependsOn('processIdeaSettings')
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

apply plugin: 'java-gradle-plugin'

dependencies {
    relocateClasses 'com.google.guava:guava'
    relocateClasses 'org.jdom:jdom'
    relocateClasses 'org.xmlunit:xmlunit-core:2.9.0'

    api('gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:1.1.6') {
        exclude group: 'com.google.code.findbugs'
        exclude group: 'org.checkerframework'
        exclude group: 'com.google.errorprone'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
    }


    testImplementation 'org.json:json:20220924'
}

Configuration saxonConf = configurations.create('saxon') {
    configurations.relocateClasses.extendsFrom(it)

    dependencies.add(project.dependencies.create('net.sf.saxon:Saxon-HE:11.4'))
}

configurations.create('saxonOptionalDependencies') {
    configurations.excludeFromClassesRelocation.extendsFrom(it)
    configurations.excludeFromForcedClassesRelocation.extendsFrom(it)

    defaultDependencies { deps ->
        saxonConf.resolvedConfiguration.lenientConfiguration.allModuleDependencies.forEach { dep ->
            if (dep.moduleGroup == 'xml-apis'
                || dep.moduleGroup == 'org.apache.httpcomponents'
                || dep.moduleGroup.startsWith('org.apache.httpcomponents.')
            ) {
                deps.add(project.dependencies.create(group: dep.moduleGroup, name: dep.moduleName, version: dep.moduleVersion))
            }
        }
    }
}

gradlePlugin {
    plugins {
        'name.remal.idea-settings' {
            id = 'name.remal.idea-settings'
            implementationClass = 'name.remal.gradleplugins.ideasettings.IdeaSettingsPlugin'
            displayName = 'Configure IntelliJ IDEA project'
            description = property('repository-description')
        }
    }
}

configurations.gradlePluginApiDependencies {
    exclude(group: 'com.google.code.findbugs')
    exclude(group: 'com.google.errorprone')
    exclude(group: 'com.google.guava')
    exclude(group: 'com.google.code.gson')
}
